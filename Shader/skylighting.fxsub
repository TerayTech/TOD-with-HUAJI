#define NUMS_SAMPLES_SUN 4

struct ScatteringParams
{
	float sunRadiance;

	float mieG;
	float mieHeight;

	float rayleighHeight;

	float3 waveLambda;
	float3 waveLambdaMie;
	float3 waveLambdaRayleigh;

	float earthRadius;
	float earthAtmTopRadius;
	float3 earthCenter;
};

float2 ComputeRaySphereIntersection(float3 position, float3 dir, float3 center, float radius)
{
	float3 origin = position - center;
	float B = dot(origin, dir);
	float C = dot(origin, origin) - radius * radius;
	float D = B * B - C;

	float2 minimaxIntersections;
	if (D < 0.0)
	{
		minimaxIntersections = float2(-1.0, -1.0);
	}
	else
	{
		D = sqrt(D);
		minimaxIntersections = float2(-B - D, -B + D);
	}

	return minimaxIntersections;
}

float ChapmanApproximation(float X, float h, float cosZenith)
{
	float c = sqrt(X + h);
	float c_exp_h = c * exp(-h);

	if (cosZenith >= 0)
	{
		return c_exp_h / (c * cosZenith + 1);
	}
	else
	{
		float x0 = sqrt(1 - cosZenith * cosZenith) * (X + h);
		float c0 = sqrt(x0);

		return 2 * c0 * exp(X - x0) - c_exp_h / (1 - c * cosZenith);
	}
}

float GetOpticalDepthSchueler(float h, float H, float earthRadius, float cosZenith)
{
	return H * ChapmanApproximation(earthRadius / H, h / H, cosZenith);
}

float2 ComputeOpticalDepth(ScatteringParams setting, float3 samplePoint, float3 V, float3 L, float neg)
{
	float rl = length(samplePoint);
	float h = rl - setting.earthRadius;
	float3 r = samplePoint / rl;

	float cos_chi_sun = dot(r, -L);
	float cos_chi_ray = dot(r, V * neg);

	float opticalDepthSun = GetOpticalDepthSchueler(h, setting.rayleighHeight, setting.earthRadius, cos_chi_sun);
	float opticalDepthCamera = GetOpticalDepthSchueler(h, setting.rayleighHeight, setting.earthRadius, cos_chi_ray) * neg;

	return float2(opticalDepthSun, opticalDepthCamera);
}

void AerialPerspective(ScatteringParams setting, float3 start, float3 end, float3 V, float3 L, bool infinite, out float3 transmittance, out float3 insctrMie, out float3 insctrRayleigh)
{
	float inf_neg = infinite ? 1 : -1;

	float3 sampleStep = (end - start) / NUMS_SAMPLES_SUN;
	float3 samplePoint = end - sampleStep;
	float3 sampleLambda = setting.waveLambdaMie + setting.waveLambdaRayleigh;

	float sampleLength = length(sampleStep);

	float3 scattering = 0;
	float2 lastOpticalDepth = ComputeOpticalDepth(setting, end, V, L, inf_neg);

	for (int i = 1; i < NUMS_SAMPLES_SUN; i++, samplePoint -= sampleStep)
	{
		float2 opticalDepth = ComputeOpticalDepth(setting, samplePoint, V, L, inf_neg);

		float3 segment_s = exp(-sampleLambda * (opticalDepth.x + lastOpticalDepth.x) * 0.5f);
		float3 segment_t = exp(-sampleLambda * (opticalDepth.y - lastOpticalDepth.y));
		
		transmittance = transmittance * segment_t;
		
		scattering = scattering * segment_t;
		scattering += exp(-(length(samplePoint) - setting.earthRadius) / setting.rayleighHeight) * segment_s;

		lastOpticalDepth = opticalDepth;
	}

	insctrMie = scattering * setting.waveLambdaMie * sampleLength;
	insctrRayleigh = scattering * setting.waveLambdaRayleigh * sampleLength;
}

bool ComputeSkyboxChapman(ScatteringParams setting, float3 eye, float3 V, float3 L, out float3 transmittance, out float3 insctrMie, out float3 insctrRayleigh)
{
	bool neg = true;

	float2 outerIntersections = ComputeRaySphereIntersection(eye, V, setting.earthCenter, setting.earthAtmTopRadius);
	clip(outerIntersections.y);

	float2 innerIntersections = ComputeRaySphereIntersection(eye, V, setting.earthCenter, setting.earthRadius);
	if (innerIntersections.x > 0)
	{
		neg = false;
		outerIntersections.y = innerIntersections.x;
	}

	eye -= setting.earthCenter;

	float3 start = eye + V * max(0, outerIntersections.x);
	float3 end = eye + V * outerIntersections.y;

	AerialPerspective(setting, start, end, V, L, neg, transmittance, insctrMie, insctrRayleigh);

	bool intersectionTest = innerIntersections.x < 0.0 && innerIntersections.y < 0.0;
	return intersectionTest;
}

float4 ComputeSkyInscattering(ScatteringParams setting, float3 eye, float3 V, float3 L)
{
	float3 insctrMie = 0.0;
	float3 insctrRayleigh = 0.0;
	float3 insctrOpticalLength = 1;
	bool intersectionTest = ComputeSkyboxChapman(setting, eye, V, L, insctrOpticalLength, insctrMie, insctrRayleigh);

	float phaseTheta = dot(V, -L);
	float phaseMie = ComputePhaseMie(phaseTheta, setting.mieG);
	float phaseRayleigh = ComputePhaseRayleigh(phaseTheta);
	float phaseNight = 1.0 - saturate(insctrOpticalLength.x * EPSILON);

	float3 insctrTotalMie = insctrMie * phaseMie;
	float3 insctrTotalRayleigh = insctrRayleigh * phaseRayleigh;

	float3 sky = insctrTotalMie + insctrTotalRayleigh;

	return float4(sky * setting.sunRadiance, phaseNight * intersectionTest);
}

float4 EncodeRGBT(float3 rgb, float range = 1024)
{
	float limits = 0;
	limits = max(max(rgb.r, rgb.g), max(rgb.b, 1e-6f));
	limits = min(limits, range);

	float4 encode;
	encode[3] = (range + 1) / range *  limits / (1 + limits);
	encode[3] = ceil(encode[3] * 255.0) / 255.0;

	float rcp = 1.0 / (encode[3] / (1.0 + 1.0 / range - encode[3]));

	encode[0] = rgb.r * rcp;
	encode[1] = rgb.g * rcp;
	encode[2] = rgb.b * rcp;

	return encode;
}

float2 SHKernel[6][9] =
{
	float2(0.75,0.5),float2(0.625,0.386182),float2(0.828613,0.613818),float2(0.828613,0.386182),float2(0.625,0.613818),float2(0.625,0.461711),float2(0.72843,0.461711),float2(0.77157,0.461711),float2(0.625,0.538289),
	float2(0.5,0),float2(0.625,0.386182),float2(0.125,0.274769),float2(0.125,0.386182),float2(0.625,0.274769),float2(0.625,0.461711),float2(0.375,0.461711),float2(0.125,0.461711),float2(0.625,0.157542),
	float2(0.5,0.5),float2(0.625,0.386182),float2(0.421387,0.613818),float2(0.421387,0.386182),float2(0.625,0.613818),float2(0.625,0.461711),float2(0.375,0.461711),float2(0.47843,0.461711),float2(0.625,0.538289),
	float2(0.25,0.5),float2(0.328613,0.386182),float2(1,0.613818),float2(1,0.386182),float2(0.328613,0.613818),float2(0.27157,0.461711),float2(0.5,0.461711),float2(1,0.461711),float2(0.27157,0.538289),
	float2(0.5,1),float2(0.625,0.725231),float2(0.125,0.5),float2(0.125,0.725231),float2(0.625,0.5),float2(0.625,0.842458),float2(0.375,0.842458),float2(0.125,0.842458),float2(0.625,0.5),
	float2(1,0.5),float2(0.921387,0.386182),float2(0.25,0.613818),float2(0.25,0.386182),float2(0.921387,0.613818),float2(0.97843,0.461711),float2(0.0215699,0.461711),float2(0.25,0.461711),float2(0.97843,0.538289),
};

float3 SHSamples(sampler tex, int i)
{
	const float diff0 = 0.35;
	const float diff1 = 0.12;

	float3 sh0 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][0], 0, 0)));
	float3 sh1 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][1], 0, 0)));
	float3 sh2 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][2], 0, 0)));
	float3 sh3 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][3], 0, 0)));
	float3 sh4 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][4], 0, 0)));
	float3 sh5 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][5], 0, 0)));
	float3 sh6 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][6], 0, 0)));
	float3 sh7 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][7], 0, 0)));
	float3 sh8 = DecodeRGBT(tex2Dlod(tex, float4(SHKernel[i][8], 0, 0)));

	return (sh0 + sh1 + sh2 + sh3 + sh4 + sh5 + sh6 + sh7 + sh8) / 9;
}

float3 SHCreateIrradiance(float3 n, float3 sh0, float3 sh1, float3 sh2, float3 sh3, float3 sh4, float3 sh5)
{
	float sh0_w = dot(n, float3(1.0,0.0,0.0)) * 0.5 + 0.5; 
	float sh1_w = dot(n, float3(0.0,1.0,0.0)) * 0.5 + 0.5;
	float sh2_w = dot(n, float3(0.0,0.0,1.0)) * 0.5 + 0.5;
	float sh3_w = 1.0 - sh0_w;
	float sh4_w = 1.0 - sh1_w;
	float sh5_w = 1.0 - sh2_w;

	sh0_w = pow(sh0_w, 4);
	sh1_w = pow(sh1_w, 4);
	sh2_w = pow(sh2_w, 4);
	sh3_w = pow(sh3_w, 4);
	sh4_w = pow(sh4_w, 4);
	sh5_w = pow(sh5_w, 4);

	float weight;

	float3 irradiance;
	irradiance  = sh0 * sh0_w; weight  = sh0_w;
	irradiance += sh1 * sh1_w; weight += sh1_w;
	irradiance += sh2 * sh2_w; weight += sh2_w;
	irradiance += sh3 * sh3_w; weight += sh3_w;
	irradiance += sh4 * sh4_w; weight += sh4_w;
	irradiance += sh5 * sh5_w; weight += sh5_w;

	return irradiance / weight;
}